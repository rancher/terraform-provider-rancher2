---
page_title: "Apps & Marketplace"
---

# Apps & Marketplace

Apps & Marketplace is a new Rancher 2.5 feature, to manage Helm chart repositories and applications in Rancher. The feature is available at Rancher `Cluster Explorer` UI, and is controlled by 2 objects:

* Repositories: These object represent helm repositories, and can be either traditional helm endpoints which have an index.yaml, or git repositories which will be cloned and can point to a specific branch. 
* Charts: These object represent helm charts contained at all Rancher, Partner and Custom repositories. Rancher tools such as Logging or Monitoring are included under the Rancher label

More info at [Rancher Apps & Marketplace](https://rancher.com/docs/rancher/v2.x/en/helm-charts/apps-marketplace/)

## New provider resources

To support this new feature on this terraform provider, 2 new resources has been added:
* `rancher2_catalog_v2` - To support repository object
* `rancher2_app_v2` - To support chart object


## rancher2_catalog_v2

This resource has the following arguments definition:

* `cluster_id` - (Required/ForceNew) The cluster id of the catalog V2 (string)
* `name` - (Required) The name of the catalog v2 (string)
* `ca_bundle` - (Optional) PEM encoded CA bundle which will be used to validate the repo's certificate (string)
* `enabled` - (Optional) If disabled the repo clone will not be updated or allowed to be installed from. Default: `true` (bool)
* `git_branch` - (Optional) Git Repository branch containing Helm chart definitions. Default `master` (string)
* `git_repo` - (Optional) The url of the catalog v2 repo (string)
* `insecure` - (Optional) Use insecure HTTPS to download the repo's index. Default: `false` (bool)
* `secret_name` - (Optional) K8s secret name to be used to connect to the repo (string)
* `secret_namespace` - (Optional) K8s secret namespace (string)
* `service_account` - (Optional) K8s service account used to deploy charts instead of the end users credentials (string)
* `service_account_namespace` - (Optional) The username to access the catalog if needed (string)
* `url` - (Optional) URL to an index generated by Helm (string)
* `annotations` - (Optional/Computed) Annotations for the catalog v2 (map)
* `labels` - (Optional/Computed) Labels for the catalog v2 (map)

### Examples

These are some examples how to use a new catalog v2:

* Create new repository using git repo and branch

```hcl
resource "rancher2_catalog_v2" "foo" {
  cluster_id = "<CLUSTER_ID>"
  name = "foo"
  git_repo = "<GIT_REPO_URL>"
  git_branch = "<GIT_BRANCH>"
}
```

* Create new repository using http url

```hcl
resource "rancher2_catalog_v2" "foo-url" {
  cluster_id = "<CLUSTER_ID>"
  name = "foo-url"
  url = "https://<CATALOG_URL>"
}
```

* Import existing repository

```hcl
## Define catalog at tf file
resource "rancher2_catalog_v2" "rancher-charts" {
  cluster_id = "<CLUSTER_ID>"
  name = "rancher-charts"
  git_repo = "https://git.rancher.io/charts"
  git_branch = "main"
}
## Import the catalog v2 data to tfstate
# terraform import rancher2_catalog_v2.rancher-charts rancher-charts
```

* Using datasource

```hcl
data "rancher2_catalog_v2" "foo" {
  cluster_id = "<CLUSTER_ID>"
  name = "foo"
}
```

## rancher2_app_v2

This resource has the following arguments definition:

* `cluster_id` - (Required/ForceNew) The cluster id of the app (string)
* `name` - (Required/ForceNew) The name of the app v2 (string)
* `namespace` - (Required/ForceNew) The namespace of the app v2 (string)
* `repo_name` - (Required) Repo name (string)
* `chart_name` - (Required) The app v2 chart name (string)
* `chart_version` - (Optional) The app v2 chart version (string)
* `project_id` - (Optional) Deploy the app v2 within project ID (string)
* `values` - (Optional) The app v2 values yaml. Yaml format is required (string)
* `cleanup_on_fail` - (Optional) Cleanup app v2 on failed chart upgrade. Default: `false` (bool)
* `disable_hooks` - (Optional) Disable app v2 chart hooks. Default: `false` (bool)
* `disable_open_api_validation` - (Optional) Disable app V2 Open API Validation. Default: `false` (bool)
* `force_upgrade` - (Optional) Force app V2 chart upgrade. Default: `false` (bool)
* `wait` - (Optional) Wait until app is deployed. Default: `true` (bool)
* `annotations` - (Optional/Computed) Annotations for the app v2 (map)
* `labels` - (Optional/Computed) Labels for the app v2 (map)

### Examples

These are some examples how to deploy some Rancher cerfified Apps v2:

**Note:** Apps v2 certified by Rancher should be installed within fixed app name and namespace defined by chart annotations, `catalog.cattle.io/release-name` and `catalog.cattle.io/namespace`. The provider will be forcing them on Rancher cerfified Apps v2 installation, but generating a diff on next `terraform apply`, if app v2 name and/or namespace is customized by user. Adjusting the values at tf file should supress the diff

**Note:** Latest app v2 version will be deployed, if `chart_version` is not provided

* `rancher-monitoring` - Deploy Rancher monitoring

```hcl
resource "rancher2_app_v2" "rancher-monitoring" {
  cluster_id = "<CLUSTER_ID>"
  name = "rancher-monitoring"
  namespace = "cattle-monitoring-system"
  repo_name = "rancher-charts"
  chart_name = "rancher-monitoring"
  chart_version = "9.4.200"
  values = <<EOF
prometheus:
  prometheusSpec:
    requests:
      cpu: "250m"
      memory: "250Mi"
EOF
}

# If you are reinstalling `rancher-monitoring` and the deployment is failing, try adding this values to app v2
#
#alertmanager:
#  alertmanagerSpec:
#    enabled: false
#    useExistingSecret: true
#    configSecret: alertmanager-rancher-monitoring-alertmanager
#Example:
#  values = &lt;&lt;EOF
#alertmanager:
#  alertmanagerSpec:
#    enabled: false
#    useExistingSecret: true
#    configSecret: alertmanager-rancher-monitoring-alertmanager
#prometheus:
#  prometheusSpec:
#    requests:
#      cpu: "250m"
#      memory: "250Mi"
```

* `rancher-istio` - Deploy Rancher istio and Rancher monitoring as requirement

```hcl
resource "rancher2_app_v2" "rancher-monitoring" {
  cluster_id = "<CLUSTER_ID>"
  name = "rancher-monitoring"
  namespace = "cattle-monitoring-system"
  repo_name = "rancher-charts"
  chart_name = "rancher-monitoring"
  chart_version = "9.4.200"
  values = <<EOF
alertmanager:
  alertmanagerSpec:
    enabled: false
    useExistingSecret: true
    configSecret: alertmanager-rancher-monitoring-alertmanager
prometheus:
  prometheusSpec:
    requests:
      cpu: "250m"
      memory: "250Mi"
EOF
}

resource "rancher2_app_v2" "rancher-istio" {
  depends_on = [rancher2_app_v2.rancher-monitoring] # Rancher-istio requires rancher-monitoring
  
  cluster_id = "<CLUSTER_ID>"
  name = "rancher-istio"
  namespace = "istio-system"
  repo_name = "rancher-charts"
  chart_name = "rancher-istio"
  chart_version = "1.7.100"
}
```

* `prometheus-federator` - Deploy Prometheus Federator

```hcl
resource "rancher2_app_v2" "prometheus-federator" {  
  cluster_id = "<CLUSTER_ID>"
  name = "prometheus-federator"
  namespace = "cattle-monitoring-system"
  repo_name = "rancher-charts"
  chart_name = "prometheus-federator"
  chart_version = "104.0.2+up0.4.2" 
  values = <<EOF
global:
  cattle:
    clusterId: <CLUSTER_ID>
    projectLabel: field.cattle.io/projectId
    psp:
      enabled: false
    systemDefaultRegistry: registry.rancher.com
    systemProjectId: <PROJECT_ID>
    url: <RANCHER_SERVER_URL>
    clusterName: custom
    rkePathPrefix: ''
    rkeWindowsPathPrefix: ''
  imagePullSecrets: []
  rbac:
    pspAnnotations: {}
    pspEnabled: true
  systemDefaultRegistry: registry.rancher.com
EOF
}

# About the variables of the values.yaml file
#
# CLUSTER_ID
# When viewing a specific cluster in the Rancher UI, the cluster ID (formatted as c-xxxxx) is visible in the browser's URL bar
# You can also get the ID through Rancher API:
#
#  curl -s "https://${RANCHER_SERVER}/v3/clusters?name=${CLUSTER_NAME}" \
#  -H 'content-type: application/json' \
#  -H "Authorization: Bearer $APITOKEN" \
#  --insecure | jq -r .data[0].id
#
#
# PROJECT_ID
# Go to Cluster Management>Explore>Cluster>Projects/Namespaces 
# then go to the ellipsis button (three dots) to the right of the project name and select "View YAML." 
# In the displayed YAML, the metadata.name field contains the Rancher Project ID (formatted as p-xxxxx)
#
#
# RANCHER_SERVER_URL
# It's the protocol and hostname of your Rancher server, e.g. https://rancher.my.org, configured during the installation with Helm
```

* `rancher-cis-benchmark` - Deploy Rancher cis benchmark

```hcl
resource "rancher2_app_v2" "rancher-cis-benchmark" {  
  cluster_id = "<CLUSTER_ID>"
  name = "rancher-cis-benchmark"
  namespace = "cis-operator-system"
  repo_name = "rancher-charts"
  chart_name = "rancher-cis-benchmark"
  chart_version = "1.0.100"
}
```

* `rancher-backup` - Deploy Rancher backup

```hcl
resource "rancher2_app_v2" "rancher-backup" {  
  cluster_id = "<CLUSTER_ID>"
  name = "rancher-backup"
  namespace = "cattle-resources-system"
  repo_name = "rancher-charts"
  chart_name = "rancher-backup"
  chart_version = "1.0.200"
  values = <<EOF
persistence:
  enabled: false
  size: 2Gi
  storageClass: '-'
  volumeName: ""
s3:
  bucketName: ""
  credentialSecretName: ""
  credentialSecretNamespace: ""
  enabled: false
  endpoint: ""
  endpointCA: ""
  folder: ""
  insecureTLSSkipVerify: false
  region: ""
EOF
}
```

* `rancher-logging` - Deploy Rancher logging

```hcl
resource "rancher2_app_v2" "rancher-logging" {  
  cluster_id = "<CLUSTER_ID>"
  name = "rancher-logging"
  namespace = "cattle-logging-system"
  repo_name = "rancher-charts"
  chart_name = "rancher-logging"
  chart_version = "3.6.000"
}
```

* Deploy Rancher Longhorn

```hcl
resource "rancher2_app_v2" "longhorn" {  
  cluster_id = "<CLUSTER_ID>"
  name = "rancher-longhorn"
  namespace = "longhorn-system"
  repo_name = "rancher-charts"
  chart_name = "longhorn"
  chart_version = "1.0.201"
}
```

* `longhorn` - Deploy Rancher Longhorn

```hcl
resource "rancher2_app_v2" "longhorn" {  
  cluster_id = "<CLUSTER_ID>"
  name = "rancher-longhorn"
  namespace = "longhorn-system"
  repo_name = "rancher-charts"
  chart_name = "longhorn"
  chart_version = "1.0.201"
  wait = true
}
```

* `rancher-gatekeeper` - Deploy OPA Gatekeeper

```hcl
resource "rancher2_app_v2" "rancher-gatekeeper" {  
  cluster_id = "<CLUSTER_ID>"
  name = "rancher-gatekeeper"
  namespace = "cattle-gatekeeper-system"
  repo_name = "rancher-charts"
  chart_name = "rancher-gatekeeper"
  chart_version = "3.1.101"
  wait = true
}
```
